import java.nio.charset.StandardCharsets

plugins {
    id "java-library"
    id "net.ltgt.errorprone" version "2.0.2"
    id "maven-publish"
}

group "net.mindustry_ddns"
version "2.1.0"

ext.versions = [
        "gson" : "2.9.0",
        "owner": "1.0.12",
        "junit": "5.8.2",
        "jackson": "2.14.2",
        "geantyref": "1.3.13"
]

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

javadoc {
    options.addStringOption("Xdoclint:none", "-quiet")
}

repositories {
    mavenCentral()
}

dependencies {
    // Libraries
    compileOnly "com.google.code.gson:gson:${versions.gson}"
    compileOnly "org.aeonbits.owner:owner-java8:${versions.owner}"
    compileOnly "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    api "io.leangen.geantyref:geantyref:${versions.geantyref}"

    // Tests
    testImplementation "com.google.code.gson:gson:${versions.gson}"
    testImplementation "org.aeonbits.owner:owner-java8:${versions.owner}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    testImplementation "io.leangen.geantyref:geantyref:${versions.geantyref}"

    testImplementation "org.junit.jupiter:junit-jupiter-params:${versions.junit}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"

    // Annotations
    errorprone 'com.google.errorprone:error_prone_core:2.12.1'
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
    options.encoding = StandardCharsets.UTF_8

    options.errorprone.with {
        disableWarningsInGeneratedCode = true
        disable "MissingSummary"
    }
}

publishing {
    publications {
        create("maven", MavenPublication) {
            from components.java

            pom {
                url = "https://github.com/mindustry-ddns-net/FileStore"

                organization {
                    name = "MDN"
                    url = "https://mindustry.ddns.net"
                }

                developers {
                    developer { id = "Phinner" }
                    developer { id = "Xasmedy" }
                }
            }
        }
    }
}
